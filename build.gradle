buildscript {
  ext {
    dependencyManagementPluginVersion = "0.6.0.RELEASE"
    kotlinVersion = "1.0.3"
    springBootVersion = "1.4.0.RELEASE"
    springCloudVersion = "Brixton.RELEASE"
  }
  repositories {
    jcenter()
    maven { url "http://repo.spring.io/snapshot" }
    maven { url "http://repo.spring.io/milestone" }
  }
  dependencies {
    classpath "org.springframework.boot:spring-boot-gradle-plugin:$springBootVersion"
    classpath "io.spring.gradle:dependency-management-plugin:$dependencyManagementPluginVersion"
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
  }
}

allprojects {
  version = "0.1.0-SNAPSHOT"
}

subprojects {
  repositories {
    jcenter()
    maven { url "http://repo.spring.io/snapshot" }
    maven { url "http://repo.spring.io/milestone" }
  }

  apply plugin: "java"
  apply plugin: "kotlin"
  apply plugin: "io.spring.dependency-management"

  dependencyManagement {
    imports {
      mavenBom "org.springframework.boot:spring-boot-starter-parent"
      mavenBom "org.springframework.cloud:spring-cloud-dependencies:$springCloudVersion"
    }
    dependencies {
      dependency "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
      dependencySet(group: "org.springframework.boot", version: springBootVersion) {
        entry("spring-boot-starter-web") {
          exclude "org.springframework.boot:spring-boot-starter-tomcat"
        }
      }
    }
  }

  sourceSets {
    main.java.srcDirs += 'src/main/kotlin'
  }
}

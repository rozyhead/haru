buildscript {
  ext {
    dependencyManagementPluginVersion = "0.6.0.RELEASE"
    kotlinVersion = "1.0.3"
    jacksonModuleKotlinVersion = "2.6.7"
    springBootVersion = "1.3.7.RELEASE"
    springCloudVersion = "Brixton.SR5"
  }
  repositories {
    jcenter()
    maven { url "http://repo.spring.io/snapshot" }
    maven { url "http://repo.spring.io/milestone" }
  }
  dependencies {
    classpath "org.springframework.boot:spring-boot-gradle-plugin:$springBootVersion"
    classpath "io.spring.gradle:dependency-management-plugin:$dependencyManagementPluginVersion"
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
  }
}

allprojects {
  version = "0.1.0-SNAPSHOT"
}

subprojects {
  repositories {
    jcenter()
    maven { url "http://repo.spring.io/snapshot" }
    maven { url "http://repo.spring.io/milestone" }
  }

  apply plugin: "java"
  apply plugin: "kotlin"
  apply plugin: 'spring-boot'
  apply plugin: "io.spring.dependency-management"

  dependencyManagement {
    imports {
      mavenBom "org.springframework.boot:spring-boot-starter-parent"
      mavenBom "org.springframework.cloud:spring-cloud-dependencies:$springCloudVersion"
    }
    dependencies {
      dependency "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
      dependency "com.fasterxml.jackson.module:jackson-module-kotlin:$jacksonModuleKotlinVersion"
    }
  }

  sourceSets {
    main.java.srcDirs += 'src/main/kotlin'
  }

  bootRepackage {
    executable = true
  }
}
